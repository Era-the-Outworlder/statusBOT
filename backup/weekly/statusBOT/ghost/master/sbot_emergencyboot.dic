OnBoot
{
	bootText
}

OnGhostChanged
{
	bootText
}

OnGhostCalled
{
	bootText
}

bootText
{
	"\0\s0\_qAn ERROR occurred. \nBooting in Emergency Mode. \nContact \_a[bugmail]bugreport@outworlderaffairs.com\_a to report bugs. \nClick balloon to see error message(s).\x"
}

OnAnchorSelectEx
{
	if reference1 == "bugmail" //if the anchor ID is this value
	{
		"\j[mailto:bugreport@outworlderaffairs.com]\e"
	}
}


OnSecondChange
{
	if !BalloonCheck
	{
		_errors = ""
		foreach GETERRORLOG; _error
		{
			_error = SPLIT(_error,":")
			_file = SPLITPATH(_error[1])[2] + SPLITPATH(_error[1])[3]
			_file = SPLIT(_file,"(")
			_line = CUTSPACE(REPLACE(_file[1],")",""))
			_file = _file[0]
			_msg = "\_?" + CUTSPACE(_error[3]) + " " + CUTSPACE(_error[4]) + "\_?"
			
			_level = ""
			if "note" _in_ _error[2]
			{_level = "note"}	
			elseif "warning" _in_ _error[2]
			{_level = "warning"}
			else
			{_level = "error"}
			
			_errors += "\f[bold,1]\f[name,arial]%(_level) in %(_file) on line %(_line):\f[default]\n%(_msg)\n\n"
		}
		
		_num = ARRAYSIZE(GETERRORLOG)
		_s = ""
		if _num != 1
		{_s = "s"}
		
		"\0\s[0]\b2\_q\![set,autoscroll,disable]%(_num) error%(_s)."
		--
		"\n\n"
		--
		_errors
		--
		"\x"
	}
}

OnKeyPress
{
	if reference0 == "e"
	{
		"\b2\_q\_?%(GETERRORLOG)\_?\x"
	}
}

BalloonCheck
{
	if ASEARCH("Status",var.req.key) != -1
	{
		_currentstatus = var.req.value[ASEARCH("Status",var.req.key)]
		if "balloon" _in_ _currentstatus
		{
			1
		}
		else
		{
			0
		}
	}
}